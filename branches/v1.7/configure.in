# This file is free software; you can redistribute it and/or modify it
# under the terms of the GNU Lesser General Public License as published
# by the Free Software Foundation; either version 2.1 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307,
# USA.


dnl Standard autotools stuff
AC_INIT(lib/mysql++.h)
AM_INIT_AUTOMAKE(mysql++, 1.7.40)	# Change this in mysql++.spec, too!
AM_CONFIG_HEADER(config.h)
AC_PROG_MAKE_SET
AC_PROG_LIBTOOL
AM_MAINTAINER_MODE


dnl Checks for build system tools
AC_PROG_CC
AC_PROG_CXX


dnl Checks for Standard C support
AC_HEADER_STDC
AC_C_CONST
AC_CHECK_FUNCS(strtol)


dnl Figure out whether/how to handle threading support, if available.
AC_ARG_ENABLE(thread-check,
		[  --enable-thread-check  Check for threads, and use if available. ],
		[ thread_check=yes ])
if test "x$thread_check" = "xyes"
then
	ACX_PTHREAD
	LIBS="$PTHREAD_LIBS $LIBS"
	CFLAGS="$CFLAGS $PTHREAD_CFLAGS"
	CC="$PTHREAD_CC"
fi


dnl Checks for libraries and local system features
AC_CHECK_HEADERS(zlib.h, AC_CHECK_LIB(z, gzread, [],
		[ AC_MSG_ERROR([zlib is required]) ]))
MYSQL_API_LOCATION
MYSQL_SHUTDOWN_ARG
AC_CHECK_LIB(intl, main)
AC_CHECK_LIB(socket, main)
AC_CHECK_LIB(nsl, main)
AM_WITH_DMALLOC()


dnl Check for Standard C++ support, and extensions.  This must be near
dnl the end, because the CPLUSPLUS directive makes autoconf use C++
dnl compiler for all subsequent tests!
AC_LANG_CPLUSPLUS
STL_SLIST_EXTENSION


dnl Decide if examples are to be built or not.
AC_ARG_ENABLE(examples,
		[  --enable-examples  Build the examples (built by default) ],
		[ build_examples=$enableval ],
		[ build_examples=yes ])
if test "x$build_examples" = xyes
then
	OPTIONAL_EXAMPLES=examples
	echo will build examples
else
	OPTIONAL_EXAMPLES= 
	echo will NOT build examples
fi
AC_SUBST([OPTIONAL_EXAMPLES])


#
# Done with checks!
#
AC_OUTPUT(Makefile lib/Makefile examples/Makefile)
